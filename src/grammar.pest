lit = @{ ASCII_DIGIT{1,18} }

summed = { "(" ~ throwsexpr ~ ")" }
atom = _{ lit | summed | "[" ~ throwsexpr ~ "]" }
primary = { un_op* ~ atom }

bin_op = _{ concat | minus | multiply | repeats | kh | kl | rh | rl | dice }
un_op = _{ dice | minus }
    concat = { "+" }
    minus = { "-" }
    multiply = { "*" }
    repeats = { "^" }
    kh = { ^"kh" }
    kl = { ^"kl" }
    rh = { ^"rh" }
    rl = { ^"rl" }
    dice = { ^"d" }


throwsexpr = { primary ~ (bin_op ~  primary)* }

throws = { throws_cmd? ~ throwsexpr }
throws_cmd = _{ ^"throws" | ^"ts" }
throw = { throw_cmd ~ throwsexpr }
throw_cmd = _{ ^"throw" | ^"t" }

help = { help_cmd ~ ( help_throw | help_throws | help_help | help_quit )? }
    help_cmd = _{ ^"help" | "?" }
    help_throw = { throw_cmd }
    help_throws = { throws_cmd }
    help_help = { help_cmd }
    help_quit = { quit_cmd }

quit = { quit_cmd }
quit_cmd = _{ ^"quit" | ^"q" }

cmd = { SOI ~ ( throws | throw | quit | help )? ~ EOI }

WHITESPACE = _{ " " | "\t" | NEWLINE }